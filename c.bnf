program   = (var_decl | func_decl)+
var_decl  = type IDENT ";"
func_decl = type IDENT "(" param ("," param)* ")" (";" | block)
param     = type IDENT
type      = IDENT "*"?
block     = "{" statement* "}"
statement = if | (expr ";") | return | define
if        = "if" "(" expr ")" block ";"
call      = IDENT "(" expr ("," expr)* ")"
return    = "return" expr ";"
define    = type IDENT ("=" expr)? ";"
expr      = unary (op unary)*
unary     = call
          | IDENT
          | INT
          | string
          | "&" IDENT
          | "(" expr ")"
op        = "+" | "-" | "&" | "|" | "^" | "<<" | ">>"
          | "==" | "!=" | "<" | ">" | "<=" | ">="
string    = "\"" (STRING_PART | ESCAPE)+ "\""
